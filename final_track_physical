%%% initialize the laser module
laser "scanpush cmd='zoneobst' "

%%% FINAL TRACK SIMULATION

%Use laser to measure x-distance
followline "bl" @v 0.25:($drivendist >= 0.01)
followline "bl" :($l7 >= 1.5)
eval sin(1.222)*$l8

%Move to box
followline "bl" @v 0.25:($motionstatus == 1)
ignoreobstacles
drive @v 0.5:($crossingblackline == 1)
ignoreobstacles
fwd 0.15

% Go backwards and pass the gate
fwd -1  
turn -90 @v 0.2
drive :($crossingblackline == 1)
$crossingblackline = 0
turnr 0.1 75
followline "bm" @v 0.5:($crossingblackline == 1)
fwd 0.25 @v 0.2
turn 90
$crossingblackline = 0
ignoreobstacles
followline "bm" @v 0.5:($crossingblackline == 1)
ignoreobstacles
fwd 0.1
ignoreobstacles
followline "bm" @v 0.35:($crossingblackline == 1)

%%%%% THIS WORKS

% Drive until first gate
followline "bm" @v 0.2 :($l0 <= 0.75)
x01 = $odox
followline "bm" :($l0 >= 0.75)
x02 = $odox
x0 = x01 + ((x02-x01)/2)
followline "bm" :($l0 <= 0.75)
x11 = $odox
followline "bm" :($l0 >= 0.75)
x12 = $odox
x1 = x11 + ((x12-x11)/2)
dist = abs((x1-x0)/2)
drive @v -0.2:($drivendist >= (abs((x12-x11)/2)+dist-0.15))
turn 90

% Drive until wall and turn 
ignoreobstacles
drive @v 0.3 :($l4 <= 0.2)
turn 90

% Follow the wall until finding second gate
xdisttogate = sin(1.222)*$l8
followwall "r" 0.30 @v 0.2 :($l8 >= 0.5)
x0 = $odox
drive :($l8 <= 0.5)
x11 = $odox
drive :($l8 >= 0.5)
x12 = $odox
x1 = x11 + ((x12-x11)/2)
dist = abs((x1-x0)/2)
drive @v -0.2:($drivendist >= (abs((x12-x11)/2)+dist-0.15))

% Turn and drive though the second gate
ignoreobstacles
turn -90 @v 0.2
drive :($drivendist >= (xdisttogate+0.30))
turn -90

% Follow the out side of the wall.
fwd 0.25 @v 0.2
followwall "r" 0.3 @v 0.4 :($l8 >= 0.5)
drive :($crossingblackline == 1)
$crossingblackline = 0

% Turn and drive through the third gate
fwd 0.15 @v 0.2
turn -90
followline "bm" @v 0.4:($drivendist >= 0.75)

% turn around
turn -180 @v 0.2
ignoreobstacles

% Follow line until white line
followline "bm" @v 0.4:($crossingblackline == 1)
$blacklinefound =0
followline "bm" :($drivendist >=0.5 & $blacklinefound == 0)
turn 30

%Move to white line and pass 2 more gates
followline "wm" @v 0.3:($crossingblackline == 1)

% Go to garage
turnr 0.2 -75 @v 0.2
followline "bm" @v 0.4 :($crossingblackline == 1)
ignoreobstacles
followline "bm" @v 0.2:($l4 <= 0.2)

% Open the door
turn 90 @v 0.2
drive :($l8 >= 0.5)
fwd 0.5
turn -90
fwd 0.4
ignoreobstacles
turn -180 @v 0.5
fwd 0.4 @v 0.2
turn 90
ignoreobstacles
drive @v 0.15:($crossingblackline == 1)
ignoreobstacles
fwd 0.2 @v 0.2
ignoreobstacles
turn 90
ignoreobstacles
followline "bm" @v 0.4:($l4 <= 0.2)